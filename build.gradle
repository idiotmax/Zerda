// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${Versions.android_gradle_plugin}"
        classpath "com.dicedmelon.gradle:jacoco-android:${Versions.dicedmelon_jacoco_plugin}"
        classpath "com.google.gms:oss-licenses:${Versions.gms_oss_licenses_plugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0.RC7-3")
}

detekt {
    version = "1.0.0.RC7-3"
    defaultProfile {
        input = "$projectDir/components"
        config = file("$projectDir/tools/detekt/detek.yml")
        filters = ".*/resources/.*,.*/build/.*"
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.21.0"
}

task ktlint(type: JavaExec, group: "verification") {
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "**/*.kt"
}